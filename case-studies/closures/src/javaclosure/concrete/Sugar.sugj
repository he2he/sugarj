package javaclosure.concrete;

import concretesyntax.Java;

import javaclosure.Closure;
import javaclosure.ToRefType;

import org.sugarj.languages.Java;

import javaclosure.Syntax;
  
/*
 * see http://javac.info/closures-v06a.html
 */
public sugar Sugar {
  desugarings
    desugar-closure-type
    desugar-closure
  
  rules
    desugar-closure-type :
         |[ #~result(~argument) ]|
      -> |[ javaclosure.Closure<? extends ~result', ? super ~argument'> ]|
    with
      result'   := <toRefType> result;
      argument' := <toRefType> argument

    desugar-closure :
         |[ #~result(~argument ~id:x) ~block:body ]|
      -> |[ new javaclosure.Closure<~result', ~argument'>() {
        	    public ~result' invoke(~argument' ~id:x)
        	      ~block:body
            } ]|
      with
        result' := <toRefType> result;
        argument' := <toRefType> argument
}  